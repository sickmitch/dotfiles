#!/bin/zsh
# install executables & completion functions
# args: <repo> ...
emulate -L zsh; setopt $_znap_opts
zmodload -Fa zsh/files b:zf_ln b:zf_mkdir

if (( $# < 1 )); then
  print -u2 'znap install: not enough arguments'
  .znap.help install
  return $(( sysexits[(i)USAGE] + 63 ))
fi

.znap.clone $@ ||
    return

local REPLY=
private -aU funcfiles=()
private bindir=~/.local/bin
private d='' link='' pat='[[:alnum:]][[:alnum:]_-]#[[:alnum:]]'

local -a repos=()
local gitdir=
..znap.repos install $@

private repo=
for repo in $repos; do
  repo=~[$repo]

  path=(
      $repo/{,bin/}$~pat(N*:h)
      $path[@]
  )
  fpath=(
      $repo/{,{[Cc]ompletions,[Ss]rc,zsh}/($~pat/)#}_$~pat(N^/:h)
      $fpath
  )

  funcfiles=( $repo/{,[Ff]unctions}/($~pat/)#}$~pat(N^/) )
  (( $#funcfiles )) &&
      autoload -Uz $funcfiles
done
