#!/bin/zsh
zmodload -F zsh/parameter p:functions

.znap.prompt.load() {
  emulate -L zsh; setopt $_znap_opts

  private setup=prompt_${theme}_setup

  [[ -v functions[$setup] ]] &&
      return

  autoload +X -Uz $setup &>/dev/null &&
      return

  unfunction $setup

  local repos=()
  local gitdir=
  ..znap.repos prompt $repo
  private dir=$gitdir/$repos[1]

  private -a file=( $dir/{,[Ff]unctions/}$setup(-.Y1) )
  if [[ -n $file ]]; then

    if autoload +X -Uz $file &>/dev/null; then
      fpath=( $file:h $fpath[@] )
      return
    fi

    unfunction $file
  fi

  file=( $dir/*$theme*.zsh*~*/*test*(-.Y1) )

  if [[ -n $file ]]; then
    eval "$setup() {
      unsetopt localoptions prompt{bang,cr,percent,sp,subst}
      PS1='%# '
      . $file[1]
      private opt=
      for opt in bang cr percent sp subst; do
        [[ -o prompt\$opt ]] &&
            prompt_opts+=( \$opt )
      done
      setopt localoptions
    }"
    return
  fi

  $setup() {
    prompt_opts=( bang cr percent sp subst )
  }
}

.znap.prompt.load "$@"
